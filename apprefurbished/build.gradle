apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'
apply plugin: 'project-report'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.androidcollider.koljadnik"
        minSdkVersion 14
        targetSdkVersion 28

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            buildConfigField "boolean", "IS_DEBUG", "true"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "IS_DEBUG", "false"
        }
    }

    flavorDimensions "mode"
    productFlavors {
        koljadnik {
            dimension "mode"
            versionCode 15
            versionName "1.5.5"
        }

        vysotsky {
            dimension "mode"
            applicationId "com.androidcollider.vysotsky"
            versionCode 10
            versionName "1.5.2"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    supportLibraryVersion = '28.0.0'
    mockitoVersion = '2.5.5'
    jUnitVersion = '4.12'
    espressoVersion = '2.2.2'
    daggerVersion = '2.5'
    daggerAnnotationVersion = '1.0'
    butterknifeVersion = '8.8.1'
    gsonVersion = '2.6.2'
    playServicesVersion = '16.0.4'
    afollestadMaterialDialogVersion = '0.9.4.4'
    crashlyticsVersion = '2.9.5'
    fabMenuVersion = '1.6.4'
    rxVersion = '2.0.2'
    rxJava = '2.0.9'
}

dependencies {

    //ui testing
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //unit testing
    testImplementation "junit:junit:$jUnitVersion"

    //mock framework
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    //android support
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v13:$supportLibraryVersion"

    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"


    //dagger2
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    compileOnly "javax.annotation:jsr250-api:$daggerAnnotationVersion"

    //field and method binding
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    //serialization
    implementation "com.google.code.gson:gson:$gsonVersion"

    //Google Play Services
    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"

    //Google Firebase services (realtime db)
    implementation "com.google.firebase:firebase-core:$playServicesVersion"
    implementation "com.google.firebase:firebase-database:$playServicesVersion"

    //material dialogs
    implementation "com.afollestad.material-dialogs:core:$afollestadMaterialDialogVersion"

    //Fabric Crashlytics (crash analytics)
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }
    implementation "com.github.clans:fab:$fabMenuVersion"

    implementation "io.reactivex.rxjava2:rxjava:$rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$rxVersion"
}
apply plugin: 'com.google.gms.google-services'